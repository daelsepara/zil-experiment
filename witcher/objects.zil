<OBJECT-TEMPLATE BORDER-POST =
	OBJECT
		(DESC "border post")
		(TEXT "a mechanism for fast travel")
		(SYNONYM POST SIGN)
		(ADJECTIVE BORDER)
		(ACTION BORDER-POST-F)>

<OBJECT CONCEPT-MORNING
	(DESC "morning")
	(SYNONYM DAYBREAK MORNING DAWN SUNRISE DAYLIGHT)
	(IN GENERIC-OBJECTS)>

<OBJECT CONCEPT-EVENING
	(DESC "evening")
	(SYNONYM NIGHTTIME EVENING DARK NIGHTFALL DUSK SUNDOWN SUNSET)
	(IN GENERIC-OBJECTS)>

<OBJECT CONCEPT-JOURNAL
	(DESC "journal")
	(IN GENERIC-OBJECTS)
	(FLAGS CONTBIT OPENBIT)>

<OBJECT CONCEPT-TOMORROW
	(DESC "tomorrow")
	(SYNONYM TOMORROW MORROW)
	(IN GENERIC-OBJECTS)
	(FLAGS NARTICLEBIT)>

<OBJECT CROW-FIGURINE
	(IN BATTLE-FIELD)
	(DESC "crow-shaped glass figurine")
	(SYNONYM CROW GLASS FIGURINE FIGURE)
	(ADJECTIVE SMALL GLASS)
	(FLAGS TAKEBIT NDESCBIT MAGICBIT)>

<OBJECT DUMMY-FOOD
    (DESC "food")
    (SYNONYM FOOD)>

<OBJECT DUMMY-BOMB
    (DESC "bombs")
    (SYNONYM BOMB BOMBS)>

<OBJECT EST-EST
	(DESC "bottle of Est est wine")
	(SYNONYM BOTTLE WINE)
	(ADJECTIVE EXPENSIVE)
	(FLAGS TAKEBIT)>

<OBJECT TORCH
	(IN SWAMP-CAVE)
	(DESC "Torch")
	(SYNONYM TORCH TORCHLIGHT)
	(FLAGS TAKEBIT LIGHTBIT)>

<OBJECT WHITE-ORCHARD-BOUNTY-BOARD
	(IN WHITE-ORCHARD-TOWN)
	(SYNONYM BOARD ANNOUNCEMENTS ANNOUNCEMENT)
	(ADJECTIVE BOUNTY BOUNTIES QUEST QUESTS CONTRACT CONTRACTS)
	(DESC "White Orchard Announcement Board")
	(TEXT "Important announcements for the town will be posted here including quests and bounties.")
	(FLAGS READBIT SURFACEBIT)>

<OBJECT ABANDONED-VILLAGE-BOUNTY-BOARD
	(IN ABANDONED-VILLAGE)
	(SYNONYM BOARD ANNOUNCEMENTS ANNOUNCEMENT)
	(ADJECTIVE BOUNTY BOUNTIES QUEST QUESTS CONTRACT CONTRACTS)
	(DESC "Announcement Board")
	(TEXT "Important announcements for the town will be posted here including quests and bounties.")
	(FLAGS READBIT SURFACEBIT VOWELBIT)>

<BORDER-POST WHITE-ORCHARD-BORDERPOST
	(IN WHITE-ORCHARD-TOWN)>

<BORDER-POST ABANDONED-VILLAGE-BORDERPOST
	(IN ABANDONED-VILLAGE)>

<ROUTINE BORDER-POST-F ("AUX" COUNT ACTIVATED KEY ITEM)
	<SET COUNT <GET BORDER-POSTS 0>>
	<COND (<VERB? ACTIVATE>
		<DO (I 1 .COUNT)
			<COND (<EQUAL? ,PRSO <GET BORDER-POSTS .I>>
				<PUT BORDER-POST-ACTIVATED .I T>
				<TELL "[" D ,PRSO " activated]" CR>
				<RTRUE>
			)>
		>
	)(<VERB? USE>
		<FLUSH>
		<SET ACTIVATED FALSE>
		<DO (I 1 .COUNT)
			<COND (<EQUAL? ,PRSO <GET BORDER-POSTS .I>>
				<SET ACTIVATED <GET BORDER-POST-ACTIVATED .I>>
			)>
		>
		<COND (.ACTIVATED
			<REPEAT ()
				<CRLF>
				<TELL "Select you destination:" CR>
				<DO (I 1 .COUNT)
					<COND (<AND <NOT <EQUAL? ,PRSO <GET BORDER-POSTS .I>>> <GET BORDER-POST-ACTIVATED .I>> <TELL N .I " - " D <GET BORDER-POST-DESTINATIONS .I> CR>)>
				>
				<TELL "0 - step away" CR>
				<SET KEY <INPUT 1>>
				<COND (<AND <G? .KEY 48> <L? .KEY 58>>
					<SET ITEM <- .KEY 48>>
					<COND (<EQUAL? ,PRSO <GET BORDER-POSTS .ITEM>>
						<TELL CR "You are already there!" CR>
					)(<GET BORDER-POST-ACTIVATED .ITEM>
						<TIME-PASSES <* 2 WITCHER-TRAVEL-TIME>>
						<TELL CR "[some time passed]" CR CR>
						<GOTO <GET BORDER-POST-DESTINATIONS .ITEM>>
						<RETURN>
					)>
				)>
				<COND (<EQUAL? .KEY !\0>
					<TELL CR "[you stepped away from " T ,PRSO "]" CR>
					<RTRUE>
				)>
			>
			<RTRUE>
		)>
	)>
	<WHAT-NOW>>
