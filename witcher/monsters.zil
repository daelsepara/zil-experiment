<CONSTANT HP-NEKKER 100>
<CONSTANT HP-BANDITS 300>

<OBJECT DUMMY-MONSTER
    (DESC "Monster")
    (SYNONYM MONSTER NEKKER NEKKERS)
    (LOC GLOBAL-OBJECTS)
    (ACTION DUMMY-MONSTER-F)
    (FLAGS MONSTERBIT)>

<ROUTINE DUMMY-MONSTER-F ("AUX" MONSTER)
    <COND (<IN? ,PLAYER ,ROACH>
        <SET MONSTER <FIND-IN <LOC ,ROACH> ,MONSTERBIT>>
    )(ELSE
        <SET MONSTER <FIND-IN ,HERE ,MONSTERBIT>>
    )>
    <COND (.MONSTER
        <COND (<IN? ,PLAYER ,ROACH>
            <COND (<VERB? ATTACK>
                <TELL "You need to dismount first!" CR>
            )(ELSE
                <PERFORM ,PRSA .MONSTER>
            )>
        )>
    )(ELSE
        <TELL "There are no monsters here!" CR>
    )>>

<OBJECT NEKKER
    (DESC "Nekker")
    (SYNONYM NEKKER NEKKERS OGRE OGROID MONSTER)
    (ADJECTIVE UGLY GROTESQUE)
    (HIT-POINTS 0)
    (HIT-DAMAGE 20)
    (ACTION NEKKER-F)
    (FLAGS MONSTERBIT)>

<OBJECT BANDITS
    (DESC "Bandits")
    (SYNONYM BANDITS THUGS)
    (HIT-POINTS 0)
    (HIT-DAMAGE 10)
    (ACTION BANDITS-F)
    (FLAGS PLURALBIT MONSTERBIT)>

<ROUTINE MONSTER-ACTION (ARGMONSTER ARGMONSTERHP ARGSWORD ARGWEAPON "OPT" ARGOIL)
    <COND (<VERB? ATTACK>
        <COND(<EQUAL? .ARGSWORD ,SILVER-SWORD>
            <COMBAT-SILVER .ARGMONSTER .ARGWEAPON .ARGOIL>
        )(ELSE
            <COMBAT-STEEL .ARGMONSTER .ARGWEAPON .ARGOIL>
        )>
    )(<AND <OR <VERB? LOOK-CLOSELY> <VERB? EXAMINE>> <EQUAL? ,PRSO .ARGMONSTER>>
        <COND(<L? <GETP .ARGMONSTER P?HIT-POINTS> .ARGMONSTERHP>
            <TELL "... " T .ARGMONSTER " appears wounded." CR>
        )>
    )(ELSE
        <MONSTER-RESPONSE .ARGMONSTER> 
    )>>

<ROUTINE NEKKER-F ()
    <MONSTER-ACTION ,NEKKER ,HP-NEKKER ,SILVER-SWORD ,PRSI ,OGROID-OIL>>

<ROUTINE BANDITS-F ()
    <MONSTER-ACTION ,BANDITS ,HP-BANDITS ,STEEL-SWORD ,PRSI ,HANGEDMANS-VENOM>>
